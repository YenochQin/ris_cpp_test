!***********************************************************************
!                                                                      *
      subroutine LODCSH2(NFILE, NCORE, JB)
!
! IMPORTANT:
! ==========
!   If jb == LOADALL, then it loads ALL blocks. In this case, the
!   NCFblock in the common should be the total ncf and allocations
!   should be changed to this ncf. Both should be done before calling
!   this routine - like the genuine block case.
!
!   Loads the data from the  .csl  file for the current block          *
!   All parameters are inputs:
!
!     nfile:    The unit number, usually 21
!     jb:       The block number (see above)
!     ncore:    The number of the core (sub-)shells.
!   Since NCFblock is known in the block version, allocation is done
!   once outside this routine. NCFblock is checked against the one (NCF)
!   obtained here.
!
!   Call(s) to: [LIB92]: CONVRT, IQ, ISPAR, ITJPO, JCUP, JQS,
!                        PACK, PARSJL, PRSRCN, PRSRSL
!                                                                      *
!   Written by Farid A. Parpia            Last revision: 23 Dec 1992   *
!   Modified by C. F. Fischer for block calculation      22 May 1997   *
!   Updated by Xinghong He                               08 Jul 1998   *
!***********************************************************************
!...Translated by Pacific-Sierra Research 77to90  4.3E  12:13:05   2/14/04
!...Modified by Charlotte Froese Fischer
!                     Gediminas Gaigalas  10/05/17
!-----------------------------------------------
!   M o d u l e s
!-----------------------------------------------
      use iso_fortran_env, only: real64, int32, int64, real128
      use parameter_def,   ONLY: NNNW
      use DEBUG_C
      use DEF_C
      use ORB_C, ncfblock => ncf
      use STAT_C
      use TERMS_C,         only: jtab, ntab
      use IOUNIT_C
!-----------------------------------------------
!   I n t e r f a c e   B l o c k s
!-----------------------------------------------
      use prsrcn_I
      use parsjl_I
      use pack_I
      use convrt_I
      use iq_I
      use jqs_I
      use jcup_I
      IMPLICIT NONE
!-----------------------------------------------
!   D u m m y   A r g u m e n t s
!-----------------------------------------------
      integer, intent(in) :: NFILE
      integer  :: NCORE
      integer, intent(in) :: JB
!-----------------------------------------------
!   L o c a l   P a r a m e t e r s
!-----------------------------------------------
      integer, parameter :: LOADALL = -119
      character*7, parameter :: MYNAME = 'LODCSH2'
      integer, parameter :: NW2 = 2*NNNW
!-----------------------------------------------
!   L o c a l   V a r i a b l e s
!-----------------------------------------------
      integer, dimension(NNNW) :: IOCC
      integer, dimension(NW2) :: IQSUB
      integer, dimension(NNNW) :: JX
      integer :: NCORP1, NREC, NCF, NPEEL, I, J
      integer :: IOS, IERR, LOC, NQS, ISPARC, NJX, IOC, IPTY
      integer :: NQSN, NJXN, NPEELN, NOPEN, JLAST, ILAST, IOCCI
      integer :: NKJI, ifULLI, NU, JSUB, IQT, NBEG, NEND
      integer :: LENTH, JXN, JPI, NCOREL, IQGG
      logical :: EMPTY, FULL
      character :: STR*256, RECL
!-----------------------------------------------
!
      if (JB /= LOADALL) then
         WRITE (6, *) 'Loading CSF File for block ', JB
      else
         WRITE (6, *) 'Loading CSF File for ALL blocks '
      endif

      NCORP1 = NCORE + 1
      NPEEL = NW - NCORE
!
! NPEEL is used as 1) number of peel orbitals (here) and
!                  2) number of peel electrons (later in this routine)
!   NREC is the sequence number of the last record read in the
!   Configuration Symmetry List File
!
      NREC = 5
!
!   There must be three records for each CSF: For instance,
!
!    4s ( 2) 4p-( 2) 4p ( 3) 4d-( 2) 4d ( 5) 4f-( 6) 4f ( 4)
!                        3/2       0     5/2             2;4
!                                           1               3-
!
!   Zero out the arrays that store packed integers - only when ncfblock>0
!
      IQA(:NNNW,:NCFBLOCK) = 0
      JQSA(:NNNW,1,:NCFBLOCK) = 0
      JQSA(:NNNW,2,:NCFBLOCK) = 0
      JQSA(:NNNW,3,:NCFBLOCK) = 0
      JCUPA(:NNNW,:NCFBLOCK) = 0

      NCF = 0
    3 CONTINUE
      NCF = NCF + 1
!
      READ (NFILE, '(A)', IOSTAT=IOS) STR

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! This if...READ makes the routine load the entire file (all blocks)
! by ignoring the end-of-block mark

      if (IOS .EQ. 0 .AND. str(1:2) .EQ. ' *' .AND. jb .EQ. LOADALL) &
        READ (nfile, '(A)', IOSTAT = IOS) str
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

      if (IOS==0 .AND. STR(1:2)/=' *') then
!
!   Read in the occupations (q) of the peel shells; stop with a
!   message if an error occurs
!
         CALL PRSRCN (STR, NCORE, IOCC, IERR)
         if (IERR /= 0) GO TO 28
!
!   Read the J_sub and v quantum numbers
!
         READ (nfile,'(A)',IOSTAT = IOS) str
         if (IOS /= 0) then
            WRITE (ISTDE, *) MYNAME//': Expecting subshell quantum', &
               ' number specification;'
            GO TO 27
         endif
         LOC = LEN_TRIM(STR)
         CALL PARSJL (1, NCORE, STR, LOC, IQSUB, NQS, IERR)
         if (IERR /= 0) GO TO 27
!
!   Read the X, J, and (sign of) P quantum numbers
!
         READ (nfile,'(A)',IOSTAT = IOS) str
         if (IOS /= 0) then
            WRITE (ISTDE, *) MYNAME//': Expecting intermediate ', &
               'and final angular momentum'
            WRITE (ISTDE, *) 'quantum number and final parity ', &
               'specification;'
            GO TO 26
         endif
!
!   Zero out the arrays that store packed integers
!
         IQA(:NNNW,NCF) = 0
         JQSA(:NNNW,1,NCF) = 0
         JQSA(:NNNW,2,NCF) = 0
         JQSA(:NNNW,3,NCF) = 0
         JCUPA(:NNNW,NCF) = 0

!   Determine the parity and all intermediate and the final
!   angular momentum quantum numbers
!
         LOC = LEN_TRIM(STR)
         RECL = STR(LOC:LOC)
         if (RECL == '+') then
            ISPARC = 1
         else if (RECL == '-') then
            ISPARC = -1
         else
            WRITE (ISTDE, *) MYNAME//': Incorrect parity ', &
                            'specification;'
            GO TO 26
         endif
         LOC = LOC - 1
!
         CALL PARSJL (2, NCORE, STR, LOC, JX, NJX, IERR)
         if (IERR /= 0) GO TO 26
!
!   Set the occupation and subshell quantum number array elements
!   in IQ, JQS for the core subshells
!
         DO I = 1, NCORE
            CALL PACK (NKJ(I) + 1, I, IQA(1:NNNW,NCF))
            CALL PACK (0, I, JQSA(1:NNNW,1,NCF))
            CALL PACK (0, I, JQSA(1:NNNW,2,NCF))
            CALL PACK (1, I, JQSA(1:NNNW,3,NCF))
         END DO
!
!   Check all subshell, intermediate and final angular momentum
!   quantum numbers; set the array elements in IQ, JQS for the peel
!   subshells; set the coupling array element in JCUP and the total
!   angular momentum array element in ITJPO
!
         IOC = 0
         IPTY = 0
         NQSN = 0
         NJXN = 0
         NPEELN = 0
         NOPEN = 0
         JLAST = 0
         ILAST = 0
         DO I = NCORP1, NW
            IOCCI = IOCC(I)
            NPEELN = NPEELN + IOCCI
            NKJI = NKJ(I)
            ifULLI = NKJI + 1
            EMPTY = IOCCI == 0
            if (.NOT.EMPTY) IOC = IOC + 1
            FULL = IOCCI == ifULLI
            if (EMPTY .OR. FULL) then
               NU = 0
               JSUB = 0
            else
               IPTY = IPTY + NKL(I)*IOCCI
               if (NKJI /= 7) then
                  NQSN = NQSN + 1
                  if (NQSN > NQS) then
                     WRITE (ISTDE, *) MYNAME//': Too few subshell ', &
                        'quantum numbers specified;'
                     GO TO 26
                  endif
                  NU = 0
                  JSUB = IQSUB(NQSN)
               else
                  if (IOCCI /= 4) then
                     NQSN = NQSN + 1
                     if (NQSN > NQS) then
                        WRITE (ISTDE, *) MYNAME//': Too few subshell ', &
                           'quantum numbers specified;'
                        GO TO 26
                     endif
                     NU = 0
                     JSUB = IQSUB(NQSN)
                  else
                     NQSN = NQSN + 1
                     if (NQSN > NQS) then
                        WRITE (ISTDE, *) MYNAME//': Too few subshell ', &
                           'quantum numbers specified;'
                        GO TO 26
                     endif
                     JSUB = IQSUB(NQSN)
                     if (JSUB==4 .OR. JSUB==8) then
                        NU = JSUB/2
                        NQSN = NQSN + 1
                        if (NQSN > NQS) then
                           WRITE (ISTDE, *) MYNAME//': Too few subshell', &
                              ' quantum numbers specified;'
                           GO TO 26
                        endif
                        JSUB = IQSUB(NQSN)
                     else
                        NU = 0
                     endif
                  endif
               endif
               IQT = MIN(IOCCI,ifULLI - IOCCI)
               LOC = (ifULLI - 2)/2
               LOC = (LOC*(LOC + 1))/2 + IQT
               NBEG = JTAB(LOC+1) + 1
               NEND = JTAB(LOC+2)
               DO J = NBEG, NEND, 3
                  if (NTAB(J+2) /= JSUB + 1) CYCLE
                  if (NU == 0) then
                     NU = NTAB(J)
                     GO TO 9
                  else
                     if (NTAB(J) == NU) GO TO 9
                  endif
               END DO
               CALL CONVRT (NP(I), STR, LENTH)
               WRITE (ISTDE, *) MYNAME//': Subshell quantum numbers ', &
                  'specified incorrectly for '//STR(1:LENTH)//NH(I)//&
                  ' subshell.'
               GO TO 26
            endif
    9       CONTINUE
            if (.NOT.EMPTY .AND. .NOT.FULL) then
               NOPEN = NOPEN + 1
               if (NOPEN > 1) then
                  if (JSUB == 0) then
                     JXN = JLAST
                  else
                     ILAST = IOC
                     NJXN = NJXN + 1
                     if (NJXN > NJX) then
                        WRITE (ISTDE, *) MYNAME//': Too few intermediate', &
                           ' and final angular momentum', &
                           ' quantum numbers specified;'
                        GO TO 26
                     endif
                     JXN = JX(NJXN)
                     DO J = ABS(JLAST - JSUB), JLAST + JSUB, 2
                        if (JXN == J) GO TO 11
                     END DO
                     CALL CONVRT (NP(I), STR, LENTH)
                     WRITE (ISTDE, *) MYNAME//': coupling of '//STR(1:LENTH)//&
                        NH(I), ' subshell to previous subshells is incorrect.'
                     GO TO 26
                  endif
   11             CONTINUE
                  CALL PACK (JXN + 1, NOPEN - 1, JCUPA(1:NNNW,NCF))
                  JLAST = JXN
               else
                  JLAST = JSUB
               endif
            endif
            CALL PACK(IOCCI, I, IQA(1:NNNW,NCF))
            CALL PACK (NU, I, JQSA(1:NNNW,1,NCF))
            CALL PACK (0, I, JQSA(1:NNNW,2,NCF))
            CALL PACK (JSUB + 1, I, JQSA(1:NNNW,3,NCF))
         END DO
!
         DO I = MAX(1,NOPEN), NW
            CALL PACK (0, I, JCUPA(1:NNNW,NCF))
         END DO
!
         if (NQSN /= NQS) then
            WRITE (ISTDE, *) MYNAME//': Too many subshell', &
               ' quantum numbers specified;'
            GO TO 26
         endif
!
         if (ILAST /= IOC) NJXN = NJXN + 1
         if (NJXN /= NJX) then
            WRITE (ISTDE, *) MYNAME//': Too many intermediate', &
               ' and final angular momentum', ' quantum numbers specified;'
            GO TO 26
         endif
!
         if (JX(NJXN) /= JLAST) then
            WRITE (ISTDE, *) MYNAME//': Final angular momentum', &
               ' incorrectly specified;'
            GO TO 26
         endif
!
         IPTY = (-1)**IPTY
         if (IPTY /= ISPARC) then
            WRITE (ISTDE, *) MYNAME//': Parity specified incorrectly;'
            GO TO 26
         endif
!
         JPI = (JLAST + 1)*IPTY
         CALL PACK (JPI, NNNW, JCUPA(1:NNNW,NCF))
!
         if (NCF > 1) then
            if (NPEELN /= NPEEL) then
               WRITE (ISTDE, *) MYNAME//': Inconsistency in the number', &
                  ' of electrons.'
               GO TO 26
            endif
         else
            NPEEL = NPEELN
         endif
!
!   Check if this CSF was already in the list; stop with a
!   message if this is the case
!
         if (NCF > 1) then
            DO J = 1, NCF - 1
               DO I = NCORP1, NW
                  if (IQ(I,J) /= IQ(I,NCF)) GO TO 17
                  if (JQS(1,I,J) /= JQS(1,I,NCF)) GO TO 17
                  if (JQS(2,I,J) /= JQS(2,I,NCF)) GO TO 17
                  if (JQS(3,I,J) /= JQS(3,I,NCF)) GO TO 17
               END DO
               DO I = 1, NOPEN - 1
                  if (JCUP(I,J) /= JCUP(I,NCF)) GO TO 17
               END DO
            END DO
            WRITE (ISTDE, *) MYNAME//': Repeated CSF;'
            GO TO 26
         endif
!
!   Successfully read a CSF; update NREC and read another CSF
!
   17    CONTINUE
         NREC = NREC + 3

         GO TO 3
!
      else                                       ! the record just read is either ' *' or EOF, marking
            ! the end of a block or end of the file
!
!   There is always at least one CSF
!
         if (NCF == 1) then
            DO I = 1, NCORE
               CALL PACK (NKJ(I) + 1, I, IQA(1:NNNW,1))
               CALL PACK (0, I, JQSA(1:NNNW,1,1))
               CALL PACK (0, I, JQSA(1:NNNW,2,1))
               CALL PACK (1, I, JQSA(1:NNNW,3,1))
            END DO
            CALL PACK (0, 1, JCUPA(1:NNNW,1))
            CALL PACK (1, NNNW, JCUPA(1:NNNW,1))
         else
            NCF = NCF - 1
         endif
!
      endif

      if (NCF /= NCFBLOCK) then
         WRITE (ISTDE, *) MYNAME//': ncf=', NCF, 'ncfblock=', NCFBLOCK
         STOP
      endif
!
!   Check if any subshell is empty; eliminate it from the
!   list if this is the case; issue a message
!
      I = NCORP1
   19 CONTINUE
      if (I <= NW) then
         DO J = 1, NCF
            if (IQ(I,J) /= 0) GO TO 23
         END DO
         CALL CONVRT (NP(I), STR, LENTH)
         WRITE (6, *) 'Subshell '//STR(1:LENTH)//NH(I)//' is empty', &
            ' in all CSFs'
   23    CONTINUE
         I = I + 1
         GO TO 19
      endif
!
!   Store the number of electrons in the COMMON variable
!   This will act as a check now - it's been determined in lodcsh
!
      NCOREL = 0
      NCOREL = SUM(NKJ(:NCORE)+1)
!      NELEC = NCOREL+NPEEL
      if (NCOREL + NPEEL /= NELEC) then
         WRITE (ISTDE, *) MYNAME//': nelec not equal to that in lodcsh'
         STOP
      endif
      WRITE (6,*)'There are ',NCF,' relativistic CSFs... load complete;'
      return
!
   26 CONTINUE
      BACKSPACE (NFILE)
   27 CONTINUE
      BACKSPACE (NFILE)
   28 CONTINUE
      BACKSPACE (NFILE)
      WRITE (ISTDE, *) ' CSF sequence number: ', NCF
      DO I = 1, 3
         READ  (nfile,'(A)',ERR = 29,END = 29) str
         WRITE (ISTDE, *) STR(1:LEN_TRIM(STR))
      END DO
   29 continue
      CLOSE(NFILE)

      STOP
      end subroutine LODCSH2
