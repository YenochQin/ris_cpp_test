!*******************************************************************
!                                                                  *
      subroutine DIAGA3(JA1,JA2,KA,IRE,IAT,REC)
!                                                                  *
!   ---------------  SECTION REC    SUBPROGRAM 03  --------------  *
!                                                                  *
!     subroutine CALLED:  IXJTIK, SIXJ                             *
!                                                                  *
!   Written by G. Gaigalas,                                        *
!   Transform to fortran 90/95 by G. Gaigalas       December 2012  *
!   The last modification made by G. Gaigalas       October  2017  *
!                                                                  *
!*******************************************************************
!
!-----------------------------------------------
!   M o d u l e s
!-----------------------------------------------
      use iso_fortran_env, only: real64, int32, int64, real128
      use CONS_C,          ONLY: ZERO, ONE
      use m_C,             ONLY: JLIST, JJQ1, JJC1, JJC2
!-----------------------------------------------
!   I n t e r f a c e   B l o c k s
!-----------------------------------------------
      use ixjtik_I
      use sixj_I
      IMPLICIT NONE
!-----------------------------------------------
!   D u m m y   A r g u m e n t s
!-----------------------------------------------
      integer, intent(in)  :: JA1, JA2, KA, IRE
!      integer, intent(out) :: IAT
      integer, INTENT(INOUT)      :: IAT
      real(kind=real64), intent(out) :: REC
!-----------------------------------------------
!   L o c a l   V a r i a b l e s
!-----------------------------------------------
      integer :: I,LL1,JI,KK1,KK2,ITI,ITI1,ITIS,ITI1S,ifAZ
      real(kind=real64) :: AA, A3
!-----------------------------------------------
      REC = ZERO
      AA=ONE
      I=JA1+1
      if(JA1 == 1)I=I+1
      if(I >= JA2) then
        REC=AA
        IAT=1
      else
    1   LL1=JLIST(I)
        JI=JJQ1(3,LL1)-1
        KK2=I-2
        ITI=JJC1(KK2)-1
        ITIS=JJC2(KK2)-1
        KK1=I-1
        ITI1=JJC1(KK1)-1
        ITI1S=JJC2(KK1)-1
        if(IRE /= 0) then
          CALL SIXJ(KA,ITIS,ITI,JI,ITI1,ITI1S,0,A3)
          A3=A3*SQRT(DBLE((ITI+1)*(ITI1S+1)))
          ifAZ=KA+JI+ITI+ITI1S
          if((ifAZ/4)*4 /= ifAZ)A3=-A3
          AA=AA*A3
        else
          if(IXJTIK(KA,ITIS,ITI,JI,ITI1,ITI1S) == 0)return
        END if
        I=I+1
        if(I == JA2) then
          REC=AA
          IAT=1
        else
          GO TO 1
        END if
      END if
      return
      end subroutine DIAGA3
