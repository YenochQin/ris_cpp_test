!*******************************************************************
!                                                                  *
      subroutine WAP1JJG(K1,BK2,QM1,QM2,QM3,IK,BK,ID,BD,WA)
!                                                                  *
!   Written by  G. Gaigalas                                        *
!   Transform to fortran 90/95 by G. Gaigalas       December 2012  *
!   The last modification made by G. Gaigalas       October  2017  *
!                                                                  *
!*******************************************************************
!
!-----------------------------------------------
!   M o d u l e s
!-----------------------------------------------
      use iso_fortran_env, only: real64, int32, int64, real128
      use CONS_C,          ONLY: ZERO, TENTH, TWO, EPS
!-----------------------------------------------
!   I n t e r f a c e   B l o c k s
!-----------------------------------------------
      use izas1_I
      use itjj3_I
      use rumtjj_I
      use ixjtik_I
      use a1jj_I
      use w1jjg_I
      use sixj_I
      IMPLICIT NONE
!-----------------------------------------------
!   D u m m y   A r g u m e n t s
!-----------------------------------------------
      integer,      intent(in)               :: K1
      integer,      intent(in), dimension(7) :: IK, ID
      real(kind=real64), intent(in)               :: BK2, QM1, QM2, QM3
      real(kind=real64), intent(in), dimension(3) :: BK, BD
      real(kind=real64), intent(out)              :: WA
!-----------------------------------------------
!   L o c a l   V a r i a b l e s
!-----------------------------------------------
      integer :: KK1, KK2, IE, IQ3, IQM, IT, ITP, ITG, IBTT
      integer,      dimension(7) :: IBT
      real(kind=real64)               :: ENQP, D1, SI1, W
      real(kind=real64), dimension(3) :: BT
!-----------------------------------------------
      WA=ZERO
      if(IZAS1(ID(7),BD(3),IK(7),BK(3)) == 0)return
      ENQP=ZERO
      KK1=K1*2
      KK2=BK2+BK2+TENTH*BK2
      IQ3=QM3*TWO+QM3*TENTH
      if(ID(3) > 37) return
      if(ITJJ3(IK,ID,KK2,BK,BD,IBT,BT,ITP,ITG,IQ3) == 0)return
      IQM=TWO*DABS(BT(3))+TENTH
      DO IT=ITP,ITG
        CALL RUMTJJ(IT,IBT(3),IBT(7),IBTT,IBT(6))
        if(IQM > IBT(7)) CYCLE
        if(IXJTIK(KK1,IK(3),KK2,ID(6),IK(6),IBT(6)) == 0) CYCLE
        IBT(1)=IT
        BT(2)=DBLE(IBT(6))/TWO
        BT(1)=DBLE(IBT(7))/TWO
        CALL A1JJ(IBT,BT,ID,BD,QM3,D1)
        if(DABS(D1) < EPS) CYCLE
        CALL W1JJG(K1,QM1,QM2,IK,BK,IBT,BT,W)
        if(DABS(W) < EPS) CYCLE
        D1=D1*W
        CALL SIXJ(KK1,IK(3),KK2,ID(6),IK(6),IBT(6),0,SI1)
        ENQP=ENQP+D1*SI1
        END DO
      WA=ENQP*DSQRT(DBLE((KK2+1)))
      IE=KK2+IK(6)+ID(6)
      if(((IE/4)*4) /= IE)WA=-WA
      return
      end subroutine WAP1JJG
