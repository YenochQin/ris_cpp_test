!*******************************************************************
!                                                                  *
      subroutine NINE0(J1,J2,J3,L1,L2,L3,K1,K2,K3,AA)
!                                                                  *
!     THIS PACKAGE DETERMINES THE VALUES OF 9j COEFFICIENT         *
!                                                                  *
!     |  J1/2  J2/2  J3/2 |                                        *
!     |  L1/2  L2/2  L3/2 |                                        *
!     |  K1/2  K2/2    0  |                                        *
!                                                                  *
!   Written by G. Gaigalas,                                        *
!   Vilnius,  Lithuania                             March    1995  *
!   Transform to fortran 90/95 by G. Gaigalas       December 2012  *
!   The last modification made by G. Gaigalas       October  2017  *
!                                                                  *
!*******************************************************************
!
!-----------------------------------------------
!   M o d u l e s
!-----------------------------------------------
      use iso_fortran_env, only: real64, int32, int64, real128
      use CONS_C,          ONLY: ZERO, ONE
!-----------------------------------------------
!   I n t e r f a c e   B l o c k s
!-----------------------------------------------
      use sixj_I
      IMPLICIT NONE
!-----------------------------------------------
!   D u m m y   A r g u m e n t s
!-----------------------------------------------
      integer  :: J1, J2, J3, L1, L2, L3, K1, K2, K3
      real(kind=real64), intent(out) :: AA
!-----------------------------------------------
!   L o c a l   V a r i a b l e s
!-----------------------------------------------
      integer      :: ifA
      real(kind=real64) :: A, B
!-----------------------------------------------
      if (J1 == 0) then
         CALL SIXJ (L2, K2, J2, K3, L3, L1, 0, A)
         B = DBLE((J2 + 1)*(L1 + 1))
         ifA = K2 + J2 + L3 + L1
      else if (J2 == 0) then
         CALL SIXJ (L3, K3, J3, K1, L1, L2, 0, A)
         B = DBLE((J3 + 1)*(L2 + 1))
         ifA = K3 + J3 + L1 + L2
      else if (J3 == 0) then
         CALL SIXJ (L1, K1, J1, K2, L2, L3, 0, A)
         B = DBLE((J1 + 1)*(L3 + 1))
         ifA = K1 + J1 + L2 + L3
      else if (L1 == 0) then
         CALL SIXJ (K2, J2, L2, J3, K3, K1, 0, A)
         B = DBLE((L2 + 1)*(K1 + 1))
         ifA = J2 + L2 + K3 + K1
      else if (L2 == 0) then
         CALL SIXJ (K3, J3, L3, J1, K1, K2, 0, A)
         B = DBLE((L3 + 1)*(K2 + 1))
         ifA = J3 + L3 + K1 + K2
      else if (L3 == 0) then
         CALL SIXJ (K1, J1, L1, J2, K2, K3, 0, A)
         B = DBLE((L1 + 1)*(K3 + 1))
         ifA = J1 + L1 + K2 + K3
      else if (K1 == 0) then
         CALL SIXJ (J2, J3, J1, L3, L2, K2, 0, A)
         B = DBLE((J1 + 1)*(K2 + 1))
         ifA = J3 + J1 + L2 + K2
      else if (K2 == 0) then
         CALL SIXJ (J3, J1, J2, L1, L3, K3, 0, A)
         B = DBLE((J2 + 1)*(K3 + 1))
         ifA = J1 + J2 + L3 + K3
      else if (K3 == 0) then
         CALL SIXJ (J1, J2, J3, L2, L1, K1, 0, A)
         B = DBLE((J3 + 1)*(K1 + 1))
         ifA = J2 + J3 + L1 + K1
      else
         A = ZERO
         B = ONE
      endif
      AA = A/DSQRT(B)
      if (MOD(ifA,4) /= 0) AA = -AA
      return
      end subroutine NINE0
